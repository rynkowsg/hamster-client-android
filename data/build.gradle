buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'me.tatarka:gradle-retrolambda:3.1.0'
  }
}

apply plugin: 'com.android.library'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'

android {
  def globalConfiguration = rootProject.extensions.getByName("ext")

  compileSdkVersion globalConfiguration.getAt("androidCompileSdkVersion")
  buildToolsVersion globalConfiguration.getAt("androidBuildToolsVersion")

  defaultConfig {
    minSdkVersion globalConfiguration.getAt("androidMinSdkVersion")
    targetSdkVersion globalConfiguration.getAt("androidTargetSdkVersion")
    versionCode globalConfiguration.getAt("androidVersionCode")
    versionName globalConfiguration.getAt("androidVersionName")
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }

  lintOptions {
    disable 'InvalidPackage' // Lombok has issue with this
  }

  dexOptions {
    incremental true // stackoverflow.com/a/30322882
  }
}

retrolambda {
  jdk System.getenv("JAVA8_HOME")
  oldJdk System.getenv("JAVA7_HOME")
  javaVersion JavaVersion.VERSION_1_7
  incremental true
}

dependencies {
  compile project(':domain')

  def dataDependencies = rootProject.ext.dataDependencies
  apt dataDependencies.daggerCompiler
  apt dataDependencies.lombok
  compile dataDependencies.commonsLang3
  compile dataDependencies.dagger
  compile dataDependencies.rxAndroid
  compile dataDependencies.rxJava
  provided dataDependencies.javaxAnnotation
  provided dataDependencies.lombok
//  compile dataDependencies.timber
//  compile dataDependencies.slf4jTimber
  compile dataDependencies.androidLogger
  compile dataDependencies.slf4jApi

  compile fileTree(dir: 'libs', include: ['*.jar'])
}
