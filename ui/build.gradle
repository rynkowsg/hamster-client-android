buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'me.tatarka:gradle-retrolambda:3.1.0'
  }
}

apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'

android {
  def globalConfiguration = rootProject.extensions.getByName("ext")

  compileSdkVersion globalConfiguration.getAt("androidCompileSdkVersion")
  buildToolsVersion globalConfiguration.getAt("androidBuildToolsVersion")

  defaultConfig {
    applicationId globalConfiguration.getAt("androidApplicationId")
    minSdkVersion globalConfiguration.getAt("androidMinSdkVersion")
    targetSdkVersion globalConfiguration.getAt("androidTargetSdkVersion")
    versionCode globalConfiguration.getAt("androidVersionCode")
    versionName globalConfiguration.getAt("androidVersionName")
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }

  lintOptions {
    disable 'InvalidPackage' // Lombok, ButterKnife: github.com/JakeWharton/butterknife/issues/110
  }

  dexOptions {
//    incremental true // stackoverflow.com/a/30322882
  }
}

retrolambda {
//  jdk System.getenv("JAVA8_HOME")
//  oldJdk System.getenv("JAVA7_HOME")
//  javaVersion JavaVersion.VERSION_1_7
//  incremental true
  jvmArgs '-noverify'
}

dependencies {
  compile project(':domain')
  compile project(':data')
  compile project(':presentation')

  def uiDependencies = rootProject.ext.uiDependencies

  compile uiDependencies.appcompatV7
  compile uiDependencies.cardViewV7
  compile uiDependencies.design
  compile uiDependencies.recyclerViewV7
  compile uiDependencies.supportAnnotations
  compile uiDependencies.supportV4

  apt uiDependencies.daggerCompiler
  apt uiDependencies.lombok
  compile uiDependencies.androidLogger
  compile uiDependencies.butterKnife
  compile uiDependencies.commonsLang3
  compile uiDependencies.dagger
  compile uiDependencies.guava
  compile uiDependencies.rxAndroid
  compile uiDependencies.rxJava
  compile uiDependencies.slf4jApi
  provided uiDependencies.lombok

}
