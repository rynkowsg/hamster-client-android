buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'me.tatarka:gradle-retrolambda:3.1.0'
  }
}

apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

retrolambda {
  jdk System.getenv("JAVA8_HOME")
  oldJdk System.getenv("JAVA7_HOME")
  javaVersion JavaVersion.VERSION_1_7
  incremental true
}

android {
  def globalConfiguration = rootProject.extensions.getByName("ext")

  compileSdkVersion globalConfiguration.getAt("androidCompileSdkVersion")
  buildToolsVersion globalConfiguration.getAt("androidBuildToolsVersion")

  defaultConfig {
    applicationId globalConfiguration.getAt("androidApplicationId")
    minSdkVersion globalConfiguration.getAt("androidMinSdkVersion")
    targetSdkVersion globalConfiguration.getAt("androidTargetSdkVersion")
    versionCode globalConfiguration.getAt("androidVersionCode")
    versionName globalConfiguration.getAt("androidVersionName")
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }

  lintOptions {
    // Lombok, ButterKnife: https://github.com/JakeWharton/butterknife/issues/110
    disable 'InvalidPackage'
  }

  dexOptions {
    incremental true         // http://stackoverflow.com/a/30322882
  }
}

dependencies {
  compile project(':domain')
  compile project(':data')

  def presentationDependencies = rootProject.ext.presentationDependencies
  compile presentationDependencies.butterKnife
  provided presentationDependencies.lombok
}
